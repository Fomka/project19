--- Краткий текст
Поставить метку удаления в записях таблицы ZPMP006_1 (по одной записи)

--- Исходный код
FUNCTION ZPMP006_1_DELETE .
*"----------------------------------------------------------------------
*"*"Локальный интерфейс:
*"  IMPORTING
*"     REFERENCE(IV_DO_COMMIT) TYPE  BOOLEAN DEFAULT ' '
*"  EXPORTING
*"     REFERENCE(O_NUM_OF_PROCESSED) TYPE  I
*"  CHANGING
*"     REFERENCE(ct_records) TYPE  ZPMP006_1_TABLE_TYPE
*"  EXCEPTIONS
*"      DB_ERROR
*"----------------------------------------------------------------------

  Data:
    ls_record like line of ct_records.

"Поставить метку удаления в записях таблицы ZPMP006_1 (по одной записи)
  Loop at ct_records into ls_record.
    ls_record-deleted = gc_true.
    ls_record-change_date = sy-datum.
    ls_record-change_time = sy-uzeit.
    ls_record-change_name = sy-uname.
    Update ZPMP006_1
      set DELETED = ls_record-deleted
          CHANGE_DATE = ls_record-change_date
          CHANGE_TIME = ls_record-change_time
          CHANGE_NAME = ls_record-change_name
      where UNIQUE_INDEX = ls_record-unique_index.
    If sy-subrc <> 0.
    "Если хотя бы одна запись не обновилась, то отменить все изменения и выйти
      Rollback work.
      Raise DB_ERROR.
    Else.
      Modify ct_records from ls_record index sy-tabix.
      Add 1 to o_num_of_processed.
    Endif.
  Endloop.

"Успешно обновлены все записи — подтвердить все изменения
  If iv_do_commit = gc_true.
    Commit work and wait.
  Endif.

Endfunction.