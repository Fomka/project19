*&---------------------------------------------------------------------*
*& Report  ZPMP001
*&
*&---------------------------------------------------------------------*
*&   Программа для ведения базы данных по программному обеспечению, входящему
*& в состав автоматизированных систем управления технологическим процессом
*& (АСУТП) и средств измерения (СИ).
*&
*----------------------------------------------------------------------------
*  Префиксы в названиях
*    g = global, глобальное объявление (не используется в подпрограммах)
*    l = local, локальное объявление
*
*    t = table, таблица
*    s = structure, структура
*    v = value, переменная
*    c = constant, константа
*    f = field-symbol, указатель
*
*    i = input, входной параметр
*    o = output, выходной параметр
*
*    ty_  = type, тип
*    ty_s = type of structure, тип структуры
*    ty_t = type of table, тип таблицы
*--------------------------------------------------------------------*
Report  ZPMP001.

Constants:
  gc_true type boolean value 'X',
  gc_false type boolean value ' ',
  begin of gc_version,
    major type n length 1 value '0',
    minor type n length 2 value '04',
  end of gc_version,
  begin of gcs,
    icon_delete type iconname value '@B_DELE@', "Метод cl_icon->get_icon для 'ICON_DELETE'
    begin of commands,
      "Общие для всех экранов
      help type sy-ucomm value 'HELP',
      version type sy-ucomm value 'VERSION',
      begin of sel_screen, "Для экрана выбора
        online type sy-ucomm value 'ONLI',
      end of sel_screen,
    end of commands,
    begin of screens,
      status type gui_status value '0001', "Статус по умолчанию
      begin of data_view, "Экран для просмотра
        number type i value 30,
        title type gui_title value '0030',
      end of data_view,
      begin of sel_screen,
        number type i value 1000,
      end of sel_screen,
    end of screens,
    begin of alv, "общее для гридов (ALV-Grid)
      begin of variant_save_modes,
        only_user type c length 1 value 'U', "Сохранять вариант только для пользователя
        only_global type c length 1 value 'X',  "только глобально — 'X'
        all type c length 1 value 'A',          "обоими способами — 'A'
      end of variant_save_modes,
      begin of stable_refresh, "при обновлении грида не сдвигать полосу прокрутки по ...
        row type lvc_s_stbl-row value gc_true, "... вертикали
        col type lvc_s_stbl-col value gc_true, "... горизонтали
      end of stable_refresh,
    end of alv,
    begin of alv_1,
      name type TABNAME value 'ZPM_DOCS',
      key_mandt type NAME_FELD value 'MANDT',
    end of alv_1,
    begin of data_table,
      table_name type TABNAME value 'ZPMP001_1',
      key_mandt type NAME_FELD value 'MANDT',
      key_stort type NAME_FELD value 'STORT',
      key_unique_index type NAME_FELD value 'UNIQUE_INDEX',
      name type NAME_FELD value 'NAME',
      manufact_place type NAME_FELD value 'MANUFACT_PLACE',
      manufact_name type NAME_FELD value 'MANUFACT_NAME',
      date_start type NAME_FELD value 'DATE_START',
      project type NAME_FELD value 'PROJECT',
      type type NAME_FELD value 'TYPE',
      version_origianl type NAME_FELD value 'VERSION_ORIGINAL',
      version_mod type NAME_FELD value 'VERSION_MOD',
      dev_environment type NAME_FELD value 'DEV_ENVIRONMENT',
      mod_cause type NAME_FELD value 'MOD_CAUSE',
      mod_worker type NAME_FELD value 'MOD_WORKER',
      mod_date type NAME_FELD value 'MOD_DATE',
      num_of_copies type NAME_FELD value 'NUM_OF_COPIES',
      commentary type NAME_FELD value 'COMMENTARY',
    end of data_table,
  end of gcs.

Types:
  ty_t_data type table of ZPMP001_1,
  ty_s_data type line of ty_t_data.

*&-------------------------------------------------------------------*
*& Обработчик событий для всех ALV-Grid'ов, объявление
*&-------------------------------------------------------------------*
Class lcl_grid_event_handler definition final.
  Public section.
    Methods:
      handle_hotspot_click
        for event hotspot_click of cl_gui_alv_grid
          importing sender e_column_id es_row_no,
      handle_data_changed
        for event data_changed of cl_gui_alv_grid
          importing sender er_data_changed.
  Private section.
    "Пусто
Endclass.

Data:
  gt_data type ty_t_data,
  gv_row_count type i,
  begin of gs_auth,
    create type boolean value gc_false,
    change type boolean value gc_false,
    view type boolean value gc_false,
    delete type boolean value gc_false,
    all type boolean value gc_false,
    alv_variant_save_mode like gcs-alv-variant_save_modes-all,
  end of gs_auth,
  begin of gs_alv,
    begin of 01,
      r_grid type ref to cl_gui_alv_grid,
      r_handler type ref to lcl_grid_event_handler,
      r_cont type ref to cl_gui_docking_container,
      t_fcat type lvc_t_fcat,
      t_sort type lvc_t_sort,
      t_excl type ui_functions,
      s_layo type lvc_s_layo,
      s_vari type disvariant,
    end of 01,
  end of gs_alv.


*&-------------------------------------------------------------------*
*& Обработчик событий для всех ALV-Grid'ов, реализация
*&-------------------------------------------------------------------*
Class lcl_grid_event_handler implementation.
  Method handle_hotspot_click.
    "Переключение между гридами
    Case sender.
      When gs_alv-01-r_grid.
*        Perform alv_1_handle_hotspot
*          using e_column_id es_row_no
*          changing gt_data.
        Message i000(SU) with 'Событие hotspot_click' 'не обрабатывается для этого ALV-Grid''а'.
      When others.
        Message i000(SU) with 'Событие hotspot_click' 'не обрабатывается для этого ALV-Grid''а'.
    Endcase.
  Endmethod.

  Method handle_data_changed.
    Case sender.
*      When gr_grid_10.
*        "Использовать экземпляр класса, передаваемый через сообщение
*        Perform alv_10_handle_data_changed using er_data_changed changing gt_equip.
      When others.
        Message i000(SU) with 'Событие data_changed' 'не обрабатывается для этого ALV-Grid''а'.
    Endcase.
  Endmethod.
Endclass.


Tables:
  ZPMP001_1.

Selection-screen begin of block b10 with frame title text-010.
  Select-options:
    so1_strt for ZPMP001_1-STORT,
    so1_name for ZPMP001_1-NAME,
    so1_man1 for ZPMP001_1-MANUFACT_PLACE,
    so1_man2 for ZPMP001_1-MANUFACT_NAME,
    so1_proj for ZPMP001_1-PROJECT,
    so1_date for ZPMP001_1-DATE_START.
Selection-screen end of block b10.
Selection-screen begin of block b20 with frame title text-020.
  Select-options:
    so2_type for ZPMP001_1-TYPE,
    so2_vero for ZPMP001_1-VERSION_ORIGINAL,
    so2_verm for ZPMP001_1-VERSION_MOD,
    so2_denv for ZPMP001_1-DEV_ENVIRONMENT,
    so2_mcau for ZPMP001_1-MOD_CAUSE,
    so2_mwor for ZPMP001_1-MOD_WORKER,
    so2_mdat for ZPMP001_1-MOD_DATE,
    so2_numc for ZPMP001_1-NUM_OF_COPIES default 1.
Selection-screen end of block b20.
Select-options:
  so3_comm for ZPMP001_1-COMMENTARY.

*--- Реакция на нажатие кнопок на экране выбора ------------------------------*
At selection-screen.
  Perform dispatch_user_command using sy-dynnr changing sy-ucomm.

*--- Нажатие "Выполнить" (F8) на экране выбора (sy-ucomm = 'ONLI') -----------*
Start-of-selection.
  Perform get_data changing gt_data gv_row_count.
  If gv_row_count = 0. "Обработать результат выборки из БД
    Message i000(SU) with 'Не найдено ни одной записи'.
    Exit.
  Else.
    Message s000(SU) with 'Записей найдено:' gv_row_count 'шт.'.
  Endif.


*--- Выборка из базы данных завершена ----------------------------------------*
End-of-selection.
  Call screen gcs-screens-data_view-number.


*--- Экранные модули, начало -------------------------------------------------*
Module init_screen output. "Общий для всех экранов — установить GUI-статус
  Set pf-status gcs-screens-status. "Установить минимальный набор функций
  "Инициировать экземпляр ALV-Grid'а и заголовок, соответствующие текущему экрану
  Case sy-dynnr.
    When gcs-screens-data_view-number.
      Set titlebar gcs-screens-data_view-title.
      Perform alv_1_display using gt_data.
    When others.
      Message i000(SU) with 'Отображение ALV-Grid''а на экране' sy-dynnr 'не обрабатывается'.
  Endcase.
Endmodule.

Module dispatch_user_command input.
  Perform dispatch_user_command using sy-dynnr changing sy-ucomm.
Endmodule.

Module exit_screen input. "Общий для всех экранов — обработать команду выхода
  Perform exit_screen using sy-dynnr.
Endmodule.
*--- Экранные модули, конец --------------------------------------------------*

*&-------------------------------------------------------------------*
*& Обработать команду от пользователя
*&-------------------------------------------------------------------*
Form dispatch_user_command
  using
    iv_dynnr type sy-dynnr
  changing
    ov_ucomm type sy-ucomm.

  Data: lv_ucomm type sy-ucomm,
        lv_buk type bukrs,
        lv_cursor_field type c length 30,
        lv_cursor_value type c length 30.

  lv_ucomm = ov_ucomm. "ov_ucomm очищается при успешном завершении подпрограммы

  If lv_ucomm is initial.
    Exit.
  Endif.

  "Искать команду в списке команд, общих для всех экранов.
  "Если найдена — обработать и выйти из этой подпрограммы.
  Case lv_ucomm.
    When gcs-commands-help.
      Perform show_help.
      Exit.
    When gcs-commands-version.
      Perform show_version using gc_version.
      Exit.
    When others.
      "Команда не найдена в списке команд, общих для всех экранов.
      "Продолжить поиск.
  Endcase.

  "Искать команду в списке команд, специфичных для указанного экрана
  Case iv_dynnr.
    "Команды на экране выбора
    When gcs-screens-sel_screen-number.
      Case lv_ucomm.
        When gcs-commands-sel_screen-online. "Нажатие "Выполнить" (F8) на экране выбора
          "ничего не делать, обработка находится в событии Start-of-selection
        When others. "Команда не найдена в списке обрабатываемых
          "Ничего не делать = стандартные команды на экране выбора и так обрабатываются
          Exit.
      Endcase.

    "Команды на остальных экранах = на экране со списком ЕО
    When others.
      Message i000(SU) with 'На экране' iv_dynnr 'не обрабатывается команда' lv_ucomm.
  Endcase.

  Clear ov_ucomm. "ov_ucomm очищается при успешном завершении подпрограммы
Endform.

Form show_help.
  Message i000(SU) with 'Скрытые команды: HELP, VERSION'.
Endform.

Form show_version using ic_version like gc_version.
  Data:
    lv_message type string,
    lv_changed_date type TRDIR-UDAT,
    lv_date_string type c length 10,
    lv_changed_by type TRDIR-UNAM,
    lv_substring type string.

  Move sy-title to lv_message.
  Concatenate lv_message ', версия '
              ic_version-major '.' ic_version-minor
    into lv_message respecting blanks.

  Select single UDAT UNAM
    from TRDIR
    into (lv_changed_date, lv_changed_by)
    where NAME = sy-repid.
  If sy-subrc <> 0.
    lv_substring = 'Последнее изменение неизвестно (таблица TRDIR).'.
  Else.
    Write lv_changed_date to lv_date_string.
    Concatenate 'Последнее изменение сделано' lv_date_string
                'пользователем' lv_changed_by
      into lv_substring separated by space.
  Endif.

  Concatenate lv_message lv_substring
    into lv_message separated by '. '.
  Message lv_message type 'I'.
Endform.

Form exit_screen
  using i_dynnr like sy-dynnr.

  Data: "Чтобы ловить исключительные ситуации
    lv_exc_text type string,
    lr_exception type ref to cx_root.

  Case i_dynnr.

    When gcs-screens-data_view-number.
      Try.
        Call method gs_alv-01-r_grid->free.
        Call method gs_alv-01-r_cont->free.
      Catch cx_root into lr_exception. "#EC CATCH_ALL
        Call method lr_exception->if_message~get_text
          receiving
            result = lv_exc_text.
        Message i000(SU) with 'Исключительная ситуация при вызове методов Free:' lv_exc_text.
      Endtry.
      Free: gs_alv-01-r_grid, gs_alv-01-r_cont, gs_alv-01-r_handler,
            gt_data, gs_alv-01-t_fcat, gs_alv-01-t_sort,
            gs_alv-01-s_layo, gs_alv-01-s_vari.

    When others.
      Message i000(SU) with 'Модуль exit_screen не обрабатывает экран' i_dynnr.
      Exit.

  Endcase.

  "Вернуться на предыдущий экран, с которого был вызван текущий
  Leave to screen 0.
Endform.

Form get_data
  changing
    ot_data type ty_t_data
    ov_count type i.
  Select *
    from ZPMP001_1
    into corresponding fields of table ot_data
    where STORT in so1_strt
      and NAME in so1_name
      and MANUFACT_PLACE in so1_man1
      and MANUFACT_NAME in so1_man2
      and DATE_START in so1_date
      and PROJECT in so1_proj
      and TYPE in so2_type
      and VERSION_ORIGINAL in so2_vero
      and VERSION_MOD in so2_verm
      and DEV_ENVIRONMENT in so2_denv
      and MOD_CAUSE in so2_mcau
      and MOD_WORKER in so2_mwor
      and MOD_DATE in so2_mdat
      and NUM_OF_COPIES in so2_numc
      and COMMENTARY in so3_comm.
  ov_count = sy-dbcnt.
Endform.

Form alv_1_prepare
  changing:
    ot_fieldcat type lvc_t_fcat
    ot_sort type lvc_t_sort
    os_layout type lvc_s_layo
    os_variant type disvariant
    ov_variant_save_mode like gcs-alv-variant_save_modes-all.

  Data:
    ls_fcat type lvc_s_fcat,
    ls_sort type lvc_s_sort.

  If gc_true = gs_auth-all.
    ov_variant_save_mode = gcs-alv-variant_save_modes-all.
  Else.
    ov_variant_save_mode = gcs-alv-variant_save_modes-only_user.
  Endif.

"Field catalog
  Call function 'LVC_FIELDCATALOG_MERGE'
    exporting
      i_structure_name             = gcs-data_table-table_name
    changing
      ct_fieldcat                  = ot_fieldcat
    exceptions
      others                       = 1
            .
  If sy-subrc <> 0.
    Message e000(SU) with 'Ошибка при заполнении fieldcat для ALV на экране' sy-dynnr 'из таблицы' gcs-data_table-table_name.
    Exit.
  Endif.

  Loop at ot_fieldcat into ls_fcat.
    "Скрыть столбцы
    If ls_fcat-fieldname = gcs-data_table-key_mandt
    or ls_fcat-fieldname = gcs-data_table-key_unique_index.
      ls_fcat-no_out = gc_true.
    Endif.

    "Определить порядок следования столбцов, удалить ненужные
*    Case ls_fcat-fieldname.
*      When gcs-doc_table-stand.
*        ls_fcat-col_pos = 1.
*
*      When gcs-doc_table-key_reference.
*        ls_fcat-hotspot = gc_true.
*        ls_fcat-col_pos = 2.
*
*      When gcs-doc_table-del_flag
*        or gcs-doc_table-del_date
*        or gcs-doc_table-del_time
*        or gcs-doc_table-del_name.
*        "Полностью убрать столбцы из грида
*        Delete ot_fieldcat index sy-tabix.
*        Continue.
*
*      When others.
*    Endcase.

    Modify ot_fieldcat from ls_fcat.
  Endloop.

*  Case gs_auth-delete.
*    When gc_true.
*      "Добавить столбец с хотспотами в виде мусорных корзин, вхождение 2 из 3
*      Loop at ot_fieldcat into ls_fcat where col_pos >= 3.
*        Add 1 to ls_fcat-col_pos.
*        Modify ot_fieldcat from ls_fcat index sy-tabix.
*      Endloop.
*      Clear ls_fcat.
*      ls_fcat-fieldname = gcs-doc_table-trash_doc.
*      ls_fcat-inttype = 'C'.
*      ls_fcat-hotspot = gc_true.
*      ls_fcat-icon = gc_true.
*      ls_fcat-scrtext_s = 'УдДокум'.
*      ls_fcat-scrtext_m = ls_fcat-scrtext_s.
*      ls_fcat-scrtext_l = 'Удалить документ'.
*      ls_fcat-tooltip = 'Удалить документ со всеми ЕО'.
*      ls_fcat-col_pos = 3.
*      Append ls_fcat to ot_fieldcat.
*
*      Loop at ot_fieldcat into ls_fcat where col_pos >= 6.
*        Add 1 to ls_fcat-col_pos.
*        Modify ot_fieldcat from ls_fcat index sy-tabix.
*      Endloop.
*      Clear ls_fcat.
*      ls_fcat-fieldname = gcs-doc_table-trash_equnr.
*      ls_fcat-inttype = 'C'.
*      ls_fcat-hotspot = gc_true.
*      ls_fcat-icon = gc_true.
*      ls_fcat-seltext = 'Удалить ЕО'.
*      ls_fcat-scrtext_s = 'УдЕО'.
*      ls_fcat-scrtext_m = 'Удалить ЕО'.
*      ls_fcat-scrtext_l = 'Удалить единицу оборудования'.
*      ls_fcat-tooltip = 'Удалить одну ЕО из документа'.
*      ls_fcat-col_pos = 6.
*      Append ls_fcat to ot_fieldcat.
*    When others.
*  Endcase.
  Sort ot_fieldcat by col_pos ascending.
"Sort
  ls_sort-spos = 1.
  ls_sort-fieldname = gcs-data_table-key_stort.
  ls_sort-down = gc_false.
  ls_sort-up = gc_true.
  Append ls_sort to ot_sort.

  ls_sort-spos = 2.
  ls_sort-fieldname = gcs-data_table-name.
  ls_sort-down = gc_false.
  ls_sort-up = gc_true.
  Append ls_sort to ot_sort.

"Layout
  os_layout-cwidth_opt = gc_true.

"Variant
  os_variant-report = sy-repid.
Endform.

Form alv_1_display
  using
    it_data type ty_t_data.

  Data: ls_data type ty_s_data.

  If gs_alv-01-r_grid is initial.
    Create object gs_alv-01-r_cont
      exporting
        repid         = sy-repid
        dynnr         = sy-dynnr
        side          = cl_gui_docking_container=>dock_at_top
        extension     = cl_gui_docking_container=>ws_maximizebox
      exceptions
        others                      = 1
        .
    If sy-subrc <> 0.
      Message e000(SU) with 'Не удалось создать экземпляр' 'docking-контейнера на экране' sy-dynnr.
      Exit.
    Endif.
    Create object gs_alv-01-r_grid
      exporting
        i_parent          = gs_alv-01-r_cont
      exceptions
        others            = 1
        .
    If sy-subrc <> 0.
      Message e000(SU) with 'Не удалось создать экземпляр ALV-Grid''а на экране' sy-dynnr.
      Exit.
    Endif.

    Perform alv_1_prepare changing gs_alv-01-t_fcat gs_alv-01-t_sort gs_alv-01-s_layo gs_alv-01-s_vari gs_auth-alv_variant_save_mode.
    Create object gs_alv-01-r_handler.
    Set handler gs_alv-01-r_handler->handle_hotspot_click for gs_alv-01-r_grid.

  "Добавить столбец с хотспотами в виде мусорных корзин, вхождение 3 из 3
*    Loop at it_data into ls_data.
*      ls_data-trash_doc = ls_data-trash_equnr = gcs-icon_delete.
*      Modify it_data from ls_data index sy-tabix.
*    Endloop.

    Call method gs_alv-01-r_grid->set_table_for_first_display
      exporting
        is_layout                     = gs_alv-01-s_layo
        is_variant                    = gs_alv-01-s_vari
        i_save                        = gs_auth-alv_variant_save_mode
      changing
        it_outtab                     = it_data
        it_fieldcatalog               = gs_alv-01-t_fcat
        it_sort                       = gs_alv-01-t_sort
      exceptions
        others                        = 1
            .
    If sy-subrc <> 0.
      Message e000(SU) with 'Исключ. ситуация при вызове метода'
                            'gs_alv-01-r_grid->set_table_for_first_display'
                            'на экране'
                            sy-dynnr.
      Exit.
    Endif.

  Else.
    Call method gs_alv-01-r_grid->refresh_table_display
      exporting
        is_stable = gcs-alv-stable_refresh.
  Endif.
Endform.